plugins {
    id("io.micronaut.application") version "4.6.1"
    id("com.gradleup.shadow") version "8.3.9"
    id("io.micronaut.aot") version "4.6.1"
    id("checkstyle")
}

version = "0.1"
group = "com.rvladimir"

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = '10.12.7'
    configFile = rootProject.file('src/main/resources/checkstyle/checkstyle.xml')
    ignoreFailures = false
}

configurations.checkstyle {
    exclude group: 'com.google.collections', module: 'google-collections'
}

// Source sets for different test types
sourceSets {
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += output + compileClasspath
    }

    e2eTest {
        java {
            srcDir 'src/e2eTest/java'
        }
        resources {
            srcDir 'src/e2eTest/resources'
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += output + compileClasspath
    }
}

// Configure test configurations to extend from test
configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
    e2eTestImplementation.extendsFrom testImplementation
    e2eTestRuntimeOnly.extendsFrom testRuntimeOnly
    e2eTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

dependencies {
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    annotationProcessor("io.micronaut.data:micronaut-data-processor")
    annotationProcessor("io.micronaut.sql:micronaut-hibernate-jpa")
    annotationProcessor("org.projectlombok:lombok")
    annotationProcessor("io.micronaut.validation:micronaut-validation-processor")
    compileOnly("org.projectlombok:lombok")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.micronaut.data:micronaut-data-hibernate-jpa")
    implementation("io.micronaut.sql:micronaut-hibernate-jpa")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    implementation("io.micronaut.flyway:micronaut-flyway")
    implementation("io.jsonwebtoken:jjwt-api:0.12.5")
    implementation("io.micronaut.validation:micronaut-validation")
    runtimeOnly("io.jsonwebtoken:jjwt-impl:0.12.5")
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.12.5")
    runtimeOnly("org.postgresql:postgresql")
    runtimeOnly("org.flywaydb:flyway-database-postgresql")
    compileOnly("io.micronaut:micronaut-http-client")
    compileOnly("io.micronaut.openapi:micronaut-openapi-annotations")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("org.yaml:snakeyaml")
    // Password hashing library (BCrypt)
    implementation("org.mindrot:jbcrypt:0.4")

    // Unit test dependencies
    testAnnotationProcessor("io.micronaut:micronaut-inject-java")
    testImplementation("io.micronaut:micronaut-http-client")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.mockito:mockito-core:5.11.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.11.0")
    testImplementation("org.assertj:assertj-core:3.25.3")
    testRuntimeOnly("com.h2database:h2")

    // Integration test dependencies
    integrationTestAnnotationProcessor("io.micronaut:micronaut-inject-java")
    integrationTestImplementation("io.micronaut:micronaut-http-client")
    integrationTestImplementation("org.junit.jupiter:junit-jupiter-api")
    integrationTestImplementation("org.junit.jupiter:junit-jupiter-params")
    integrationTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    integrationTestImplementation("org.assertj:assertj-core:3.25.3")
    integrationTestImplementation("org.testcontainers:testcontainers:2.0.2")
    integrationTestImplementation("org.testcontainers:postgresql:1.20.4")
    integrationTestImplementation("org.testcontainers:junit-jupiter:1.20.4")
    integrationTestImplementation("io.micronaut.testresources:micronaut-test-resources-client")

    // E2E test dependencies
    e2eTestAnnotationProcessor("io.micronaut:micronaut-inject-java")
    e2eTestImplementation("io.micronaut:micronaut-http-client")
    e2eTestImplementation("org.junit.jupiter:junit-jupiter-api")
    e2eTestImplementation("org.junit.jupiter:junit-jupiter-params")
    e2eTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    e2eTestImplementation("org.assertj:assertj-core:3.25.3")
    e2eTestImplementation("org.testcontainers:testcontainers:2.0.2")
    e2eTestImplementation("org.testcontainers:postgresql:1.20.4")
    e2eTestImplementation("org.testcontainers:junit-jupiter:1.20.4")
    e2eTestImplementation("io.micronaut.testresources:micronaut-test-resources-client")
}


application {
    mainClass = "com.rvladimir.Application"
}
java {
    sourceCompatibility = JavaVersion.toVersion("21")
    targetCompatibility = JavaVersion.toVersion("21")
}


graalvmNative.toolchainDetection = false

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.rvladimir.*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
        replaceLogbackXml = true
    }
}


tasks.named("dockerfileNative") {
    jdkVersion = "21"
}

tasks.register("installPreCommitGitHook") {
    description = "Installs the pre-commit git hook"
    group = "build setup"
    // Skip hook installation in CI environments
    onlyIf { !System.getenv('CI') }

    doLast {
        def targetFile = file("$rootDir/.git/hooks/pre-commit")
        targetFile.text = file("$rootDir/pre-commit").text
        targetFile.setExecutable(true, false)
    }
}

tasks.register("installPrePushGitHook") {
    description = "Installs the pre-push git hook"
    group = "build setup"
    // Skip hook installation in CI environments
    onlyIf { !System.getenv('CI') }

    doLast {
        def targetFile = file("$rootDir/.git/hooks/pre-push")
        targetFile.text = file("$rootDir/pre-push").text
        targetFile.setExecutable(true, false)
    }
}

tasks.named("compileJava") {
    dependsOn("installPreCommitGitHook", "installPrePushGitHook")
}

// Configure duplicate handling for resource processing
tasks.named('processIntegrationTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processE2eTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Task for integration tests
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

// Task for E2E tests
tasks.register('e2eTest', Test) {
    description = 'Runs end-to-end tests.'
    group = 'verification'
    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath
    shouldRunAfter integrationTest

    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}
